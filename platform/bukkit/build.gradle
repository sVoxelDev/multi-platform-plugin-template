apply plugin: 'kr.entree.spigradle'

archivesBaseName = "${project.property("pluginName")}-bukkit"

spigot {
    name = project.property("pluginName")
    authors = [project.property("author")]
    apiVersion = project.property("apiVersion")
    load = STARTUP
    depends = []
    softDepends = ['PlaceholderAPI']
    permissions {
        'template.player' {
            description 'Contains all basic player permissions.'
            defaults 'true'
            children = [
                    'template.player.one'        : true,
                    'template.player.two.example': true
            ]
        }
        'template.admin' {
            description 'Contains all admin permissions.'
            defaults 'op'
            children = [
                    'template.admin.reload': true
            ]
        }
    }
}

dependencies {
    implementation project(':platform')

    implementation spigot(mcVersion)

    implementation 'cloud.commandframework:cloud-paper:1.6.1'
    implementation "net.kyori:adventure-platform-bukkit:4.0.1"
    implementation "org.spongepowered:configurate-yaml:4.1.2"

    testImplementation 'com.github.seeseemelk:MockBukkit-v1.18:2.85.2'
    testImplementation(testFixtures(project(":core")))
}

shadowJar {
    archiveClassifier.set("")
    dependencies {
        include(project(':api'))
        include(project(':core'))
        include(project(':platform'))
        include(dependency('cloud.commandframework::'))
        include(dependency('io.leangen.geantyref::'))
        include(dependency('org.spongepowered::'))
        include(dependency('net.kyori::'))
    }
    relocate 'cloud.commandframework', "${packageName}.lib.commands"
    relocate 'org.spongepowered.configurate', "${packageName}.lib.configurate"
    relocate 'io.leangen.geantyref', "${packageName}.lib.typetoken"
    relocate 'net.kyori', "${packageName}.lib.kyori"
    relocate 'org.bstats', "${packageName}.lib.bstats"
}

tasks.build.dependsOn(shadowJar)
tasks.publish.dependsOn(shadowJar)